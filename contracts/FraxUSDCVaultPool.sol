//SPDX-License-Identifier: MIT
pragma solidity ^0.8.11;

import "./interfaces/IAlluoPool.sol";

import "@openzeppelin/contracts-upgradeable/security/PausableUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/access/AccessControlUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/proxy/utils/Initializable.sol";
import "@openzeppelin/contracts-upgradeable/proxy/utils/UUPSUpgradeable.sol";

import "@openzeppelin/contracts-upgradeable/utils/structs/EnumerableSetUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/token/ERC20/extensions/IERC20MetadataUpgradeable.sol";
import "@openzeppelin/contracts-upgradeable/token/ERC20/utils/SafeERC20Upgradeable.sol";

import "./interfaces/IExchange.sol";
import "./interfaces/ICvxBooster.sol";
import "./interfaces/ICvxBaseRewardPool.sol";
import "./interfaces/ICurvePool.sol";

import "hardhat/console.sol"; 


contract FraxUSDCVaultPool is Initializable, PausableUpgradeable, AccessControlUpgradeable, UUPSUpgradeable {

    ICvxBooster public constant cvxBooster =
        ICvxBooster(0xF403C135812408BFbE8713b5A23a04b3D48AAE31);
    IExchange public constant exchange =
        IExchange(0x29c66CF57a03d41Cfe6d9ecB6883aa0E2AbA21Ec);


    bytes32 public constant  UPGRADER_ROLE = keccak256("UPGRADER_ROLE");
    bytes32 public constant  VAULT = keccak256("VAULT");

    bool public upgradeStatus;

    IERC20MetadataUpgradeable rewardToken;
    IERC20MetadataUpgradeable entryToken;
    EnumerableSetUpgradeable.AddressSet yieldTokens;
    address public curvePool;
    uint256 public poolId;
    address public vault;
    using AddressUpgradeable for address;
    using SafeERC20Upgradeable for IERC20MetadataUpgradeable;
    using EnumerableSetUpgradeable for EnumerableSetUpgradeable.AddressSet;
    /// @custom:oz-upgrades-unsafe-allow constructor
    constructor() initializer {}

  function initialize(
        IERC20MetadataUpgradeable _rewardToken,
        address _multiSigWallet,
        address[] memory _yieldTokens,
        address _curvePool,
        uint256 _poolId,
        address _vault,
        IERC20MetadataUpgradeable _entryToken
    ) public initializer {
        __Pausable_init();
        __AccessControl_init();
        __UUPSUpgradeable_init();
        rewardToken = _rewardToken;
        curvePool = _curvePool;
        poolId = _poolId;
        entryToken = _entryToken;
        for (uint256 i; i < _yieldTokens.length; i++) {
            yieldTokens.add(_yieldTokens[i]);
        }
        require(_multiSigWallet.isContract(), "BaseAlluoPool: Not contract");
        _grantRole(DEFAULT_ADMIN_ROLE, _multiSigWallet);
        _grantRole(UPGRADER_ROLE, _multiSigWallet);


        // TESTS ONLY:
        _grantRole(DEFAULT_ADMIN_ROLE, msg.sender);
        _grantRole(UPGRADER_ROLE, msg.sender);
        _grantRole(VAULT, msg.sender);


        vault = _vault;
        _grantRole(VAULT, _vault);

    }

    /// @notice Claims all rewards, exchange all rewards for LPs and stake them
    /// @dev Exchanges all rewards (including those sent by the vault) for the entryToken, adds liquidity for LP tokens and then stakes them
    ///      This function is not to be called directly, but rather through the vault contract it is linked to.
    function farm() onlyRole(VAULT) external {
        // Claim rewards
        claimRewardsFromPool();
        for (uint256 i; i < yieldTokens.length(); i++) {
            address token = yieldTokens.at(i);
            uint256 balance = IERC20MetadataUpgradeable(token).balanceOf(address(this));
            if (token != address(entryToken) && balance > 0) {
                IERC20MetadataUpgradeable(token).safeIncreaseAllowance(address(exchange), balance);
                balance = exchange.exchange(token, address(entryToken), balance, 0);
            }
        }
        uint256 entryTokenBalance = IERC20MetadataUpgradeable(entryToken).balanceOf(address(this));
        if (entryTokenBalance > 0) {
            entryToken.safeIncreaseAllowance(curvePool, entryTokenBalance);
            ICurvePool(curvePool).add_liquidity([0, entryTokenBalance], 0);
            rewardToken.safeIncreaseAllowance(address(cvxBooster), rewardToken.balanceOf(address(this)));
            cvxBooster.deposit(poolId, rewardToken.balanceOf(address(this)), true);
        }
    }

    /// @notice Simply stakes all LP tokens if for some reason they are not staked
    function depositIntoBooster() external {
        rewardToken.safeIncreaseAllowance(address(cvxBooster), rewardToken.balanceOf(address(this)));
        cvxBooster.deposit(poolId, rewardToken.balanceOf(address(this)), true);
    }
    
    /// @notice Unstakes from convex and sends it back to the vault to allow withdrawals of principal
    /// @param amount Amount of lpTokens to unwrap
    function withdraw(uint256 amount) external onlyRole(VAULT) {
        (, , , address pool, , ) = cvxBooster.poolInfo(poolId);
        ICvxBaseRewardPool(pool).withdrawAndUnwrap(amount, true);
        rewardToken.safeTransfer(vault, amount);
    }

    /// @notice Returns total amount staked. 
    /// @dev Used to calculate total amount of assets locked in the vault
    /// @return uint256 balance of staked tokens
    function fundsLocked() external view returns (uint256) {
        (,,, address rewardPool,,) =  cvxBooster.poolInfo(poolId);
        return ICvxBaseRewardPool(rewardPool).balanceOf(address(this));
    }

    /// @notice Claims all rewards from the convex pool
    /// @dev This is used to claim rewards when looping
    function claimRewardsFromPool() public {
        (,,, address rewardPool,,) =  cvxBooster.poolInfo(poolId);
         ICvxBaseRewardPool(rewardPool).getReward();
    }

    function changeUpgradeStatus(bool _status)
        external
        onlyRole(DEFAULT_ADMIN_ROLE)
    {
        upgradeStatus = _status;
    }

    function pause() external onlyRole(DEFAULT_ADMIN_ROLE) {
        _pause();
    }

    function unpause() external onlyRole(DEFAULT_ADMIN_ROLE) {
        _unpause();
    }


    function grantRole(bytes32 role, address account)
        public
        override
        onlyRole(getRoleAdmin(role))
    {
        if (role == DEFAULT_ADMIN_ROLE) {
            require(account.isContract(), "IbAlluo: Not contract");
        }
        _grantRole(role, account);
    }

    function _authorizeUpgrade(address)
        internal
        override
        onlyRole(UPGRADER_ROLE)
    {
        require(upgradeStatus, "IbAlluo: Upgrade not allowed");
        upgradeStatus = false;
    }

    
}